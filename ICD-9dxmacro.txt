%macro MakeMHDxFile(year, outds);
*file MHdx&year is persisted in worklib for later use;

%let startdate = mdy(1, 1, &year) ;
%let enddate  = mdy(12, 31, &year) ;

proc sql ;
  create table worklib.dxes&year as
  select *, compress(dx, '.') as dx_comp
  from &_vdw_dx  
  where  adate between &startdate and &enddate  
  	and enctype not in ('LO', 'RO')
  order by 1, 2
  ;
quit;

data dx_raw;
	set worklib.dxes&year;
	length dx_class $40.;
	  if( 
		dx_comp in: ( '2962', '2963', '29682', '2980', '3004', '3090', '30112',  '3091', '30928' '311' )  
		or dx_comp in:('2961', '2964', '2965', '2966', '2967', '29680', '29681', '29689')
		or dx_comp in:('3000', '3002', '30920', '30921', '30924', '30981' )
		or dx_comp in:('2990', '2991', '2998' )
		or dx_comp in:('314' )
		or dx_comp in:('295', '2971', '2973', '2988', '2989', '30122')
		or dx_comp in:('E95', 'E98')
		or dx_comp in: ( '291', '292', '303', '304', '305' )
		or dx_comp in:('290' )
		);
	if dx_comp in: ('30393', '30303', '29285', '30493', '30483', '30473', '30543', '30453', '30443', '30433', '30543', '30413', '30403',
			'30593', '30583', '30573', '30563', '30553', '30543', '30533', '30523', '30513', '30503', '3051') then delete;
	if dx_comp in: ( '291', '292', '303', '304', '305' ) 	then dx_class = 'Substance Abuse Disorder'; 
	if dx_comp in: ( '2962', '2963', '29682', '2980', '3004', '3090', '30112',  '3091', '30928' '311' ) 
		then dx_class = 'Depressive Disorder'; 
	else if  dx_comp in:('2961', '2964', '2965', '2966', '2967', '29680', '29681', '29689')
		then dx_class = 'Bipolar Spectrum Disorder';
	else if dx_comp in:('3000', '3002', '30920', '30921', '30924', '30981' )
		then dx_class = 'Anxiety Disorder';
	else if  dx_comp in:('314' ) then dx_class = 'Attention Deficit Disorder';
	else if dx_comp in:('2990', '2991', '2998' ) then dx_class = 'Autism Spectrum Disorder';
	else if  dx_comp in:('295') then dx_class = 'Schizophrenia Spectrum Disorder';
	else if  dx_comp in:('290', '78093') then dx_class = 'Dementia';
	else if dx_comp in:('2971', '2973', '2988', '2989', '30122') then dx_class = 'Other Psychosis';
	else if dx_comp in:('E95') then do;
		if dx_comp ='E959' then delete;
		else dx_class = 'Self-Inflicted Injury';
	end;
	else if dx_comp in:('E98') then do;
		if dx_comp ='E989' then delete;
		ELSE dx_class = 'Possibly Self-Inflicted Injury';
	end;
run;

proc sql;
	create table dx_dept as
	select d.*, u.department
	from dx_raw d inner join &_vdw_utilization u
	on d.mrn = u.mrn and d.adate = u.adate
	and d.enctype = u.enctype and d.provider = u.provider
	;
	create table dx_spec as
	select d.*, specialty as Prov_spec 
	from dx_dept d left outer join &_vdw_provider_specialty s
	on d.provider = s.provider
	;
quit;

*clean up provider and department - make zero-length string missing;
data &outds&year;
	set dx_spec;
	year=&year;
	if compress(department, ' ') = '' then department = .;
	if compress(prov_spec, ' ') = '' then prov_spec = .;
run;

proc datasets; delete dx_tmp1;delete dx_raw;delete dx_dept; delete dx_spec;
run;

proc contents data=&outds&year;
run;

%mend;